
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		// Заполнение шапки
		Компьютер = ДанныеЗаполнения.Компьютер;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		Ответственный = ПолучитьОтветственногоЗаСборку(ДанныеЗаполнения);

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.НачалоСборки") Тогда
		// Заполнение шапки
		ДокументОснование = ДанныеЗаполнения.ДокументОснование;
		Компьютер = ДанныеЗаполнения.Компьютер;
		Ответственный = ДанныеЗаполнения.Ответственный;
	КонецЕсли;
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Функция ПолучитьОтветственногоЗаСборку(Заказ)
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачалоСборки.Ответственный
		|ИЗ
		|	Документ.НачалоСборки КАК НачалоСборки
		|ГДЕ
		|	НачалоСборки.ДокументОснование = &Заказ
		|	И НачалоСборки.Проведен";

	Запрос.УстановитьПараметр("Заказ", Заказ);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Возврат ВыборкаДетальныеЗаписи.Ответственный;
		
	Иначе
		
		Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
		
	КонецЕсли;

	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	
КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ЗаполнитьНаборЗаписей_КомплектющиеВСборке();
	
	Если Движения.КомплектющиеВСборке.Количество() = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В сборке нет комплектующих по этому заказу! Проверте, создавался ли и проведен ли документ ""Начало сборки"" на основании текущего заказа";
		Сообщение.Сообщить();
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если Не Отказ Тогда 
		
		ЗаполнитьНаборЗаписей_ГотовыеКомпьютеры();
		ЗаполнитьНаборЗаписей_НачисленияСотрудникам();
		
	КонецЕсли;
	
	Если Отказ Тогда 
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Документ " + Ссылка + " не проведен!";
		Сообщение.Сообщить();
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей_ГотовыеКомпьютеры()
	
	Движения.ГотовыеКомпьютеры.Записывать = Истина;
	
	Движение = Движения.ГотовыеКомпьютеры.Добавить();
	
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	
	Движение.Компьютер = Компьютер;
	Движение.Заказ = ДокументОснование;
	
	Движение.Количество = 1;
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей_НачисленияСотрудникам()
	
	Движения.НачисленияСотрудникам.Записывать = Истина;
	
	Движение = Движения.НачисленияСотрудникам.Добавить();
	
	Движение.Период = Дата;
	
	Движение.Компьютер = Компьютер;
	Движение.ФизическоеЛицо = Ответственный;
	
	Движение.Количество = 1;
	Движение.Сумма = Компьютер.ПремияЗаСборку;
	
КонецПроцедуры

Процедура ЗаполнитьНаборЗаписей_КомплектющиеВСборке()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КомплектющиеВСборкеОстатки.Комплектующая,
	|	КомплектющиеВСборкеОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.КомплектющиеВСборке.Остатки(&МоментВремени, Заказ = &ДокументОснование) КАК КомплектющиеВСборкеОстатки";

	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.КомплектющиеВСборке.Добавить();
		
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		
		Движение.Заказ = ДокументОснование;
		Движение.Комплектующая = ВыборкаДетальныеЗаписи.Комплектующая;
		
		Движение.Количество = ВыборкаДетальныеЗаписи.Количество;

	КонецЦикла;

	Движения.КомплектющиеВСборке.Записывать = Истина;
	
	
КонецПроцедуры
