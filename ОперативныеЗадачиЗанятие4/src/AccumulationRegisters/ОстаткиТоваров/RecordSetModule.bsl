
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Количество() = 0 Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("МенеджерВТ") = Ложь Тогда
	
		Возврат;
	
	КонецЕсли;
	
	//Ссылка = Отбор.Получить(0).Значение;
	Ссылка = Отбор.Регистратор.Значение;
    МенеджерВТ = ДополнительныеСвойства.МенеджерВТ;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваров.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваров.Свободно КАК Свободно
		|ПОМЕСТИТЬ ВТПередЗаписью
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров КАК ОстаткиТоваров
		|ГДЕ
		|	ОстаткиТоваров.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если Количество() = 0 Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("МенеджерВТ") = Ложь Тогда
	
		Возврат;
	
	КонецЕсли;
	
	//Ссылка = Отбор.Получить(0).Значение;
	Ссылка = Отбор.Регистратор.Значение;
    МенеджерВТ = ДополнительныеСвойства.МенеджерВТ;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваров.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваров.Свободно КАК Свободно
		|ПОМЕСТИТЬ ВТПослеЗаписи
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров КАК ОстаткиТоваров
		|ГДЕ
		|	ОстаткиТоваров.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПередЗаписью.Номенклатура,
		|	-1 * ВТПередЗаписью.Свободно
		|ИЗ
		|	ВТПередЗаписью КАК ВТПередЗаписью
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПослеЗаписи.Номенклатура КАК Номенклатура,
		|	СУММА(ВТПослеЗаписи.Свободно) КАК Свободно
		|ПОМЕСТИТЬ ВТТоварыДокумента
		|ИЗ
		|	ВТПослеЗаписи КАК ВТПослеЗаписи
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПослеЗаписи.Номенклатура
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВТПослеЗаписи.Свободно) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПередЗаписью
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПослеЗаписи";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Запрос.Выполнить();
	
	
КонецПроцедуры
